# Poisson likelihood, log link
# Random effects model for multi-arm trials

model {

   ## priors
   for(i in 1:ns) {                  # studies
        mu[i] ~ dnorm(0, 0.0001)     # vague priors for all trial baselines
   }

   d[1] <- 0       # reference treatment
   for (k in 2:nt) {           # treatment effects
        d[k] ~ dnorm(0, 0.0001) 
        lrr[k-1] <- d[k]
   }	

   sd ~ dunif(0,5)     # vague prior for between-trial SD
   tau <- pow(sd,-2)   # between-trial precision = (1/between-trial variance)

   ## likelihood
   for(i in 1:ns) {                 # studies
        w[i,1] <- 0                 # adjustment for multi-arm trials is zero for control arm
        delta[i,1] <- 0             # treatment effect is zero for control arm
        for (k in 1:na[i]) {             # arms
                r[i,k] ~ dpois(theta[i,k])   # binomial likelihood
                theta[i,k] <- lambda[i,k]*E[i,k]
	           log(lambda[i,k]) <- mu[i] + delta[i,k]
                # Deviance contribution
                dev[i,k] <- 2*((theta[i,k] - r[i,k]) + r[i,k]*log(r[i,k]/theta[i,k]))
        }
        
        # summed residual deviance contribution for this trial
        resdev[i] <- sum(dev[i,1:na[i]])

    for (k in 2:na[i]) {     # arms
        # trial-specific LOR distributions
        delta[i,k] ~ dnorm(md[i,k],taud[i,k])
        # mean of LOR distributions (with multi-arm trial correction)
        md[i,k] <-  d[t[i,k]] - d[t[i,1]] + sw[i,k]
        # precision of LOR distributions (with multi-arm trial correction)
        taud[i,k] <- tau*2*(k-1)/k
        # adjustment for multi-arm RCTs
        w[i,k] <- (delta[i,k] - d[t[i,k]] + d[t[i,1]])
        # cumulative adjustment for multi-arm trials
        sw[i,k] <- sum(w[i,1:k-1])/(k-1)
      }
  }

  totresdev <- sum(resdev[])     # Total Residual Deviance
}             
