# Survival analysis - fixed effects analysis (A)
# intended for use on survival data to estimate hazard ratios but can
# be used for synthesising any constant treatment effects

model {

# Define Prior Distributions
# On tx effect mean

  beta[1] <- 0
  for (tt in 2:nTx) {
    beta[tt] ~ dnorm(mu_beta, prec_beta)
  }

# On individual study baseline effect
  for(ss in 1:nStudies){
    alpha[ss] ~ dnorm(mu_alpha, prec_alpha)
  }

# Fit data
# For hazard ratio reporting studies
  for(ii in 1:LnObs) {
    Lmu[ii] <- alpha[Lstudy[ii]]*multi[ii] + beta[Ltx[ii]] - beta[Lbase[ii]]
    Lprec[ii] <- 1/pow(Lse[ii],2)
    Lmean[ii] ~ dnorm(Lmu[ii],Lprec[ii])

    # Residual deviance for hazard ratio reporting studies
	  Ldev[ii] <- pow((Lmean[ii] - Lmu[ii]), 2)*Lprec[ii]
  }

# Total residual deviances
totresdev <- sum(Ldev[1:LnObs])

# Calculate HRs
  for (hh in 1:nTx) {
    hr[hh] <- exp(beta[hh])
  }

# Ranking plot
  for (ll in 1:nTx) {
    for (mm in 1:nTx) {
      rk[ll,mm] <- equals(ranked(beta[], mm), beta[ll])
    }
  }
}
